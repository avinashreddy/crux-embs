ext {
  raptureVersion = '3.0.4'
  libsVersion = '1.0.0'
  c24Version = '4.9.5'
}

task wrapper(type: Wrapper) {
   gradleVersion = '3.2.1'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'application'

  repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "http://repo.marketcetera.org/maven"
    }
  }

  dependencies {
    compile "com.crux.embs:Common:$libsVersion"
    testCompile 'junit:junit:4.12'
    testCompile 'org.xmlunit:xmlunit-core:2.3.0'
  }

  task copyConfigFiles (type: Copy) {
    from 'src/main/resources';
    include 'rapture/config/*.cfg'
    include '**/*.bindings'
    into 'build/config'
    outputs.upToDateWhen { false }
  }

  compileJava {
    dependsOn copyConfigFiles
  }

  jar.excludes = ['rapture']

  task configDir {
    outputs.dir file("$buildDir/config")
  }

  applicationDistribution.from(configDir) { into "etc" }

  startScripts {
    classpath = files(['[REPLACEME]'])
    doLast {
      // gradle always puts extra classpaths relative to the lib folder, so we have to replace
      windowsScript.text = windowsScript.text.replace('%APP_HOME%\\lib\\[REPLACEME]', '%APP_HOME%\\lib\\*;%APP_HOME%\\etc')
      unixScript.text = unixScript.text.replace('$APP_HOME/lib/[REPLACEME]', '$APP_HOME/lib/*:$APP_HOME/etc')
    }
  }
}
